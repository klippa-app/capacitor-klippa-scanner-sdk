def DEFAULT_COMPILE_SDK_VERSION = 35
def DEFAULT_BUILD_TOOLS_VERSION = '35.0.0'
def DEFAULT_MIN_SDK_VERSION = 25
def DEFAULT_TARGET_SDK_VERSION = 35

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
    ext.kotlin_version = '2.2.21'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.6.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven {
            credentials {
                username System.getenv()['KLIPPA_SCANNER_SDK_USERNAME'] ? System.getenv()['KLIPPA_SCANNER_SDK_USERNAME'] : (keystoreProperties["klippa.scanner.sdk.username"] ? keystoreProperties["klippa.scanner.sdk.username"] : '')
                password System.getenv()['KLIPPA_SCANNER_SDK_PASSWORD'] ? System.getenv()['KLIPPA_SCANNER_SDK_PASSWORD'] : (keystoreProperties["klippa.scanner.sdk.password"] ? keystoreProperties["klippa.scanner.sdk.password"] : '')
            }
            url System.getenv()['KLIPPA_SCANNER_SDK_URL'] ? System.getenv()['KLIPPA_SCANNER_SDK_URL'] : (keystoreProperties["klippa.scanner.sdk.url"] ? keystoreProperties["klippa.scanner.sdk.url"] : 'https://custom-ocr.klippa.com/sdk/android/maven')
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace "com.klippa.capacitorscanner"
    compileSdkVersion safeExtGet('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)
    buildToolsVersion safeExtGet('buildToolsVersion', DEFAULT_BUILD_TOOLS_VERSION)

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    
    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', DEFAULT_MIN_SDK_VERSION)
        targetSdkVersion safeExtGet('targetSdkVersion', DEFAULT_TARGET_SDK_VERSION)
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = "21"
    }

    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false
    }
}

dependencies {
    def fallbackKlippaScannerVersion = "4.3.3"
    def klippaScannerVersion = project.hasProperty('klippaScannerVersion') ? project.klippaScannerVersion : fallbackKlippaScannerVersion
    implementation "com.klippa:scanner:$klippaScannerVersion"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':capacitor-android')
    implementation "androidx.appcompat:appcompat:1.6.0"
}
